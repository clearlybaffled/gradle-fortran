plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'eclipse'
    id 'jacoco'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
}

group = "io.github.clearlybaffled"
version = '0.1-SNAPSHOT'

// Add a source set for the integration test suite
configurations {
    integrationTestImplementation {
        extendsFrom(configurations.testImplementation)
    }
}

sourceSets {
   integrationTest {
      groovy.srcDir file('src/integrationTest/groovy')
      resources.srcDir file('src/integrationTest/resources')
      compileClasspath += sourceSets.main.output + configurations.testRuntime
      runtimeClasspath += output + compileClasspath
   }

}

dependencies {

    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'

    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'

    testImplementation gradleTestKit()
    testImplementation ('junit:junit:4.12') {
        exclude module: "hamcrest"
    }
    testImplementation "commons-io:commons-io:2.7"

    integrationTestImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier = "sources"
}

// Add a task to run the integration tests
task integrationTest(type: Test) {
    group = "verification"
    description = "Runs the integration tests."
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    finalizedBy jacocoTestReport
}

// Run the integration tests as part of `check`
check.dependsOn(tasks.integrationTest)

// Reports and Docs
jacocoTestReport {
    dependsOn test, integrationTest
    sourceSets sourceSets.main
    
    reports {
        xml.enabled true
        csv.enabled false
    }
}

tasks.withType(Test) {
    finalizedBy jacocoTestReport
}

task totalCoverage(type: JacocoMerge) {
    executionData file("${project.buildDir}/jacoco/test.exec"), file("${project.buildDir}/jacoco/integrationTest.exec")
    
}

task jacocoCombinedReport(type: JacocoReport) {
    dependsOn totalCoverage
    executionData file("$buildDir/jacoco/totalCoverage.exec")
    sourceSets sourceSets.main
}

groovydoc {
    exclude "**/internal/**"
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'javadoc'
    from groovydoc.destinationDir
}


// Plugin Spec
gradlePlugin {
    // Define the plugin
    plugins {
       fortran {
          id = 'io.github.clearlybaffled.fortran'
          implementationClass = 'io.github.clearlybaffled.gradle.language.fortran.FortranPlugin'
       }
    }
    testSourceSets sourceSets.integrationTest
}

 // Publishing
publishing {
    repositories {
        mavenLocal()
    }
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar
            artifact groovydocJar
        }
    }
}

// Local Development hack for eclipse to link sources to classes in gradle-api
wrapper {
   distributionType = Wrapper.DistributionType.ALL
}

plugins.withType(EclipsePlugin) {
    plugins.withType(JavaBasePlugin) {
        eclipse {
            classpath {
                file {
                    whenMerged { classpath ->
                        String gradleHome = gradle.getGradleHomeDir()
                            .absolutePath
                            .replace(File.separator, '/')
                        String gradleSourceDirectory = "${gradleHome}/src"
                        classpath.entries.each { entry ->
                            if (entry in org.gradle.plugins.ide.eclipse.model.AbstractLibrary
                                    && entry.library.path.contains('generated-gradle-jars')) {
                                entry.sourcePath =
                                    new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory()
                                        .fromPath(gradleSourceDirectory)
                            }
                        }
                    }
                }
            }
        }
    }
}
